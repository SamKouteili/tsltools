module TSL.Command.Theorize (command) where

import Options.Applicative (Parser, ParserInfo, action, auto, fullDesc, header, help, helper, info, long, metavar, option, optional, progDesc, short, showDefault, strOption, value)
import qualified TSL.ModuloTheories as ModuloTheories
import qualified TSL.Preprocessor as Preprocessor
import TSL.Utils (readInput, writeOutput)

data Options = Options
  { inputPath :: Maybe FilePath,
    outputPath :: Maybe FilePath,
    solverPath :: FilePath,
    numModels :: Int
  }

optionsParserInfo :: ParserInfo Options
optionsParserInfo =
  info (helper <*> optionsParser) $
    fullDesc
      <> progDesc "Spec (TSL) -> theory-encoded (Base TSL)"
      <> header "tsl theorize"

optionsParser :: Parser Options
optionsParser =
  Options
    <$> optional
      ( strOption $
          long "input"
            <> short 'i'
            <> metavar "FILE"
            <> help "Input file (STDIN, if not set)"
            <> action "file"
      )
    <*> optional
      ( strOption $
          long "output"
            <> short 'o'
            <> metavar "FILE"
            <> help "Output file (STDOUT, if not set)"
            <> action "file"
      )
    <*> strOption
      ( long "solver"
          <> value "cvc5"
          <> showDefault
          <> metavar "SOLVER"
          <> help "Path to SMT and SyGus solver"
          <> action "file"
      )
    <*> option auto
      ( long "SYGUS-NUMMODELS"
          <> value 3
          <> showDefault
          <> metavar "N"
          <> help "Number of PBE models for SyGuS Eventually queries"
      )

theorize :: Options -> IO ()
theorize (Options {inputPath, outputPath, solverPath, numModels}) = do
  -- Read input
  input <- readInput inputPath

  -- user-provided TSLMT spec (String) -> desugared TSLMT spec (String)
  preprocessedSpec <- Preprocessor.preprocess input

  -- desugared TSLMT spec (String) -> theory-encoded TSL spec (String)
  theorizedSpec <- ModuloTheories.theorize solverPath numModels preprocessedSpec

  -- Write to output
  writeOutput outputPath theorizedSpec

command :: ParserInfo (IO ())
command = theorize <$> optionsParserInfo
